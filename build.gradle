buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  ext {
    // Implementation Versions
    kotlin_version = "1.3.20"
    dagger_version = "2.21"
    jackson_version = "2.8.5"
    lambda_core_version = "1.2.0"
    lambda_log4j2_version = "1.1.0"
    lambda_events_version = "2.2.5"

    // Test Versions
    junit_version = "4.11"
    mockito_version = "2.23.4"
    hamcrest_version = "2.1"
  }
}

plugins {
  id "java"
  id "maven"
  id "org.jetbrains.kotlin.jvm" version "1.3.20"
  id "org.jetbrains.kotlin.kapt" version "1.3.20"
  id "com.github.johnrengelman.shadow" version "4.0.4"
  id "io.gitlab.arturbosch.detekt" version "1.0.0.RC9.2"
  id "org.jlleitschuh.gradle.ktlint" version "6.3.1"
}

group = "com.bahadirakin"
version = "1.0"

description = """
    An example project for AWS Lambda+Kotlin+Dagger
"""

repositories {
  jcenter()
  maven { url "http://repo.maven.apache.org/maven2" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
  options.encoding = "UTF-8"
}

compileTestJava {
  options.encoding = "UTF-8"
}

shadowJar{
  classifier "all"
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

kapt {
  useBuildCache = true
  includeCompileClasspath = false
  arguments {
    arg("dagger.formatGeneratedSource", "disabled")
    arg("dagger.gradle.incremental", "true")
  }
}

detekt {
  config = files("$projectDir/detekt-config.yml")
  reports {
    html {
      enabled = true
      destination = file("build/reports/detekt.html")
    }
  }
}

dependencies {
  implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: "$kotlin_version"

  implementation group: "com.amazonaws", name: "aws-lambda-java-core", version: "$lambda_core_version"
  implementation group: "com.amazonaws", name: "aws-lambda-java-log4j2", version: "$lambda_log4j2_version"
  implementation group: "com.amazonaws", name: "aws-lambda-java-events", version: "$lambda_events_version"

  implementation group: "com.fasterxml.jackson.core", name: "jackson-core", version: "$jackson_version"
  implementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "$jackson_version"
  implementation group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "$jackson_version"

  implementation group: "com.google.dagger", name: "dagger", version: "$dagger_version"
  kapt group: "com.google.dagger", name: "dagger-compiler", version: "$dagger_version"

  testImplementation group:"junit", name:"junit", version: "$junit_version"
  testImplementation group:"org.jetbrains.kotlin", name:"kotlin-test-junit", version: "$kotlin_version"
  testImplementation group:"org.mockito", name:"mockito-core", version: "$mockito_version"
  testImplementation group:"org.hamcrest", name:"hamcrest", version: "$hamcrest_version"
}

task deploy(type: Exec, dependsOn: "shadowJar") {
  commandLine "serverless", "deploy"
}
